<# 

#
# Domain and Contexts.
#

create domain 'ecommerce';
create context 'shopping' in 'ecommerce';

#
# Values, Entities and Aggregates.
#

in context 'shopping'
{
	create value 'quantity' as (single) with validators (
		greater-than(0), 
		less-than (100)
	);

	create value 'products' as (index);

	create entity 'cart' (id, member_id) as (identifier, identifier);
	create entity 'product' (id, quantity) as (identifier, value\quantity);

	create aggregate 'cart';
};

#
# Events, Commands and State.
#

in context 'shopping' in aggregate 'cart'
{
	create event 'created';
	create event 'empty';
	create event 'full';
	create event 'checked-out';

	create event 'product-added' (product) as (entity\product);
	create event 'product-quantity-changed' (product) as (entity\product);
	create event 'product-removed' (product_id) as (identifier);

	create command 'create' (cart) as (entity\cart);
	create command 'checkout' (cart_id) as (identifier);

	create command 'add-product' (cart_id, product) as (identifier, entity\product);
	create command 'change-product-quantity' (cart_id, product) as (identifier, entity\product);
	create command 'remove-product' (cart_id, product_id) as (identifier, identifier);

	create state 'state' (is_created, is_checked_out, cart, products) 
		as (boolean, boolean, entity\cart, value\products)
		with defaults (false, false, null, new value\products)
	;
};

#
# Projections
#

in context 'shopping' in aggregate 'cart'
{
	create projection 'active-carts' (identifier cart_id, identifier member_id)
	{
		add handler when event\created
		{
			insert into 'active-carts' (cart_id, member_id) 
				values (cart->id, cart->member_id)
			;
		};

		add handler when event\checked-out'
		{
			delete from 'active-carts' where 'cart_id' = cart_id;
		};
	};
};

#
# Invariants.
#

in context 'shopping' in aggregate 'cart'
{
	create invariant 'created' (cart\state state)
	{
		state->is_created == true;
	};

	create invariant 'empty' (cart\state state)
	{
		state->products->count == 0;
	};

	create invariant 'full' (cart\state state)
	{
		state->products->count >= 10;
	};

	create invariant 'checked-out' (cart\state state)
	{
		state->products->count >= 10;
	};

	create invariant 'product-exists' (cart\state state, identity product_id)
	{
		state->products->exists(product_id);
	};

	create invariant 'has_active_cart' (identifier member_id)
	{
		cart_count = count from 'active-carts' where id = member_id;

		return cart_count > 0;
	};
};

#
# State event handlers.
#

in context 'shopping' in aggregate 'cart' in state 'state'
{
	add handler when event\created
	{
		is_created = true;
	};

	add handler when event\product-added
	{
		products->add(event->product);
	};

	add handler when event\product-quantity-changed
	{
		products->replace(event->product);
	};

	add handler when event\product-removed
	{
		products->remove(event->product_id);
	};

	add handler when event\checked-out
	{
		is_checked_out = true;
	};
};

#
# Aggregate command handlers.
#

in context 'shopping' in aggregate 'cart'
{
	add handler when command\create
	{
		assert not invariant\created;
		assert invariant\has_active_cart (command->cart->member_id);

		apply event\created (command->cart);
		apply event\empty;
	};

	add handler when command\checkout
	{
		assert not invariant\checked-out;

		apply event\checkout;
	};

	add handler when command\add-product
	{
		assert not invariant\checked-out;
		assert not invariant\full;
		assert not invariant\product-exists ( command->product->id );

		apply event\product-added ( command->product );

		check invariant\full then
		{
			apply event\full;
		};
	};

	add handler when command\change-product-quantity
	{
		assert not invariant\checked-out;
		assert invariant\product-exists ( command->product->id );

		apply event\product-quantity-changed ( command->product );
	};

	add handler when command\remove-product
	{
		assert not invariant\checked-out;
		assert invariant\product-exists ( command->product->id );

		apply event\product-removed ( command->product->id );

		check invariant\empty then
		{
			apply event\empty;
		};
	};
};

#>
